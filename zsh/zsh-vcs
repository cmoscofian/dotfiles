autoload -Uz vcs_info

add-zsh-hook precmd vcs_info

zstyle ':vcs_info:*' enable git 
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{154}●%f'
zstyle ':vcs_info:*' unstagedstr '%F{160}●%f'
zstyle ':vcs_info:*' get-revision true
# zstyle ':vcs_info:*' debug true
zstyle ':vcs_info:git+set-message:*' hooks git-untracked git-stashed
zstyle ':vcs_info:git*:*' formats '[%c%u%F{213}%.7i%f@%F{213}%b%f%m] '
zstyle ':vcs_info:git*:*' actionformats '[%c%u%F{213}%b|%a%f@%F{213}%.7i%f%m] '

function +vi-git-untracked() {
	emulate -L zsh
	if [[ $(git status --porcelain 2> /dev/null | grep '^??' | wc -l) -ne 0 ]]; then
		hook_com[unstaged]+='%F{214}●%f'
	fi
}

function +vi-git-stashed() {
	emulate -L zsh
	if git rev-parse --verify refs/stash &> /dev/null; then
		hook_com[misc]+='%F{160}#%f'
	fi
}

