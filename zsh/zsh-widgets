function tmux::new-window-widget() {
    local __sources=( work private study )
    local __source
    local __project
    local __name

    if [[ -z $TMUX ]]; then
        zle reset-prompt
        print::error "This needs to be run inside of a tmux session"
        return
    fi

    __source=$(echo $__sources | tr ' ' '\n' | fzf --reverse --ansi --height 15)
    case $__source in
        study) __source=$STUDY_PROJECTS;;
        private) __source=$PRIVATE_PROJECTS;;
        *) __source=$WORK_PROJECTS;;
    esac

    __project=$(find $__source -mindepth 1 -maxdepth 1 -type d 2> /dev/null | fzf --reverse --ansi --height 15)
    if [[ -z $__project ]]; then
        zle reset-prompt
        return
    fi

    __name=$(basename $__project | tr . _)
    tmux new-window -c $__project -n $__name nvim .
    zle redisplay
}

zle -N tmux::new-window-widget
bindkey -M emacs "^K" tmux::new-window-widget
bindkey -M vicmd "^K" tmux::new-window-widget
bindkey -M viins "^K" tmux::new-window-widget
